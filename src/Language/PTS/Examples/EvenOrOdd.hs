#ifdef LANGUAGE_PTS_HAS_EQUALITY
#ifdef LANGUAGE_PTS_HAS_BOOL
#ifdef LANGUAGE_PTS_HAS_NAT
#ifdef LANGUAGE_PTS_HAS_SIGMA
#ifdef LANGUAGE_PTS_HAS_PROP
#define EQUALITY_EXAMPLES
{-# LANGUAGE OverloadedStrings, ScopedTypeVariables #-}
#endif
#endif
#endif
#endif
#endif
module Language.PTS.Examples.EvenOrOdd (
#ifdef EQUALITY_EXAMPLES
    evenOrOddScript,
#endif
    ) where

#ifdef EQUALITY_EXAMPLES
import Language.PTS
import Language.PTS.Bound

-- |
--
-- >>> runLoud $ spec_ (MartinLof 0) >> evenOrOddScript
-- -- 1. double
-- ------------
-- --
-- Î»Â» :define succ : â„• â†’ â„• = Î» n â†’ S n
-- Î»Â» :define double
-- : â„• â†’ â„• = Î» n â†’ â„•-elim (Î» _ â†’ â„•) 0 (Î» _ m â†’ succ (succ m)) n
-- --
-- Î»Â» :example double 0
-- â†ª 0 : â„•
-- --
-- Î»Â» :example double 1
-- â†ª 2 : â„•
-- --
-- Î»Â» :example double 5
-- â†ª 10 : â„•
-- --
-- -- 2. Even
-- ----------
-- --
-- Î»Â» :define Even
-- : â„• â†’ ð“¤ = Î» n â†’ âˆ‘ (m : â„•) â†’ Eq â„• n (double m)
-- Î»Â» :define zero-is-even : Even 0 = pair 0 refl
-- --
-- Î»Â» :example pair 1 refl : Even 2
-- â†ª pair 1 refl
-- : âˆ‘ (m : â„•) â†’
--   Eq â„• 2 (â„•-elim (Î» _ â†’ â„•) 0 (Î» _ m â†’ S (S m)) m)
-- --
-- -- 3. Odd
-- ---------
-- --
-- Î»Â» :define Odd
-- : â„• â†’ ð“¤ = Î» n â†’ âˆ‘ (m : â„•) â†’ Eq â„• n (S (double m))
-- --
-- Î»Â» :example pair 3 refl : Odd 7
-- â†ª pair 3 refl
-- : âˆ‘ (m : â„•) â†’
--   Eq â„• 7 (S (â„•-elim (Î» _ â†’ â„•) 0 (Î» _ m â†’ S (S m)) m))
-- --
-- -- 4. Congruence
-- ----------------
-- --
-- Î»Â» :define congruence
-- : âˆ€ A â†’
--   âˆ€ B â†’
--   âˆ (f : (A â†’ B)) â†’
--   âˆ (x : A) â†’
--   âˆ (y : A) â†’
--   Eq A x y â†’
--   Eq B (f x) (f y)
-- = Î» A B f x y p â†’
--       J A (Î» u v _ â†’ Eq B (f u) (f v)) (Î» _ â†’ refl) x y p
-- --
-- -- 5. Succ Even is Odd
-- ----------------------
-- --
-- Î»Â» :define succ-even-is-odd
-- : âˆ (n : â„•) â†’ Even n â†’ Odd (S n)
-- = Î» n even-n â†’
--       match even-n
--             (Î» m proof â†’
--                  pair m (congruence
--                              â„• â„• succ n (double m) proof))
-- Î»Â» :define succ-odd-is-even
-- : âˆ (n : â„•) â†’ Odd n â†’ Even (S n)
-- = Î» n odd-n â†’
--       match
--           odd-n
--           (Î» m proof â†’
--                pair
--                    (succ m)
--                    (congruence
--                         â„• â„• succ n (succ (double m)) proof))
-- --
-- -- 6. Boolean
-- -------------
-- --
-- Î»Â» :define ifâ‚ : âˆ (r : ð“¤â‚) â†’ ð”¹ â†’ r â†’ r â†’ r
--                = Î» r b t f â†’ ð”¹-elim (Î» _ â†’ r) t f b
-- --
-- -- 7. Either
-- ------------
-- --
-- -- 7.1. Type
-- --
-- Î»Â» :define Either
-- : ð“¤ â†’ ð“¤ â†’ ð“¤ = Î» A B â†’ âˆ‘ (t : ð”¹) â†’ ifâ‚ ð“¤ t A B
-- --
-- -- 7.2. Constructors
-- --
-- Î»Â» :define left : âˆ€ A â†’ âˆ€ B â†’ A â†’ Either A B
--                 = Î» A B x â†’ pair true x
-- Î»Â» :define right : âˆ€ A â†’ âˆ€ B â†’ B â†’ Either A B
--                  = Î» A B y â†’ pair false y
-- --
-- -- 7.3. Non-dependent elimination
-- --
-- Î»Â» :define either
-- : âˆ€ A â†’ âˆ€ B â†’ âˆ€ C â†’ (A â†’ C) â†’ (B â†’ C) â†’ Either A B â†’ C
-- = Î» A B C f g e â†’
--       match e (Î» t v â†’ (ð”¹-elim (Î» tâ‚ â†’ ifâ‚ ð“¤ tâ‚ A B â†’ C)
--                                (Î» x â†’ f x)
--                                (Î» y â†’ g y)
--                                t)
--                            v)
-- --
-- -- 8. All number are either even or odd
-- ---------------------------------------
-- --
-- Î»Â» :define theorem
-- : âˆ (n : â„•) â†’ Either (Even n) (Odd n)
-- = Î» n â†’
--       â„•-elim
--           (Î» m â†’ Either (Even m) (Odd m))
--           (pair true zero-is-even)
--           (Î» l H â†’
--                either
--                    (Even l)
--                    (Odd l)
--                    (Either (Even (succ l)) (Odd (succ l)))
--                    (Î» p â†’ pair false (succ-even-is-odd l p))
--                    (Î» p â†’ pair true (succ-odd-is-even l p))
--                    H)
--           n
-- --
-- Î»Â» :example theorem 0
-- â†ª pair true (pair 0 refl)
-- : âˆ‘ (t : ð”¹) â†’
--   ð”¹-elim
--       (Î» _ â†’ ð“¤)
--       (âˆ‘ (m : â„•) â†’
--        Eq â„• 0 (â„•-elim (Î» _ â†’ â„•) 0 (Î» _ m â†’ S (S m)) m))
--       (âˆ‘ (m : â„•) â†’
--        Eq â„• 0 (S (â„•-elim (Î» _ â†’ â„•) 0 (Î» _ m â†’ S (S m)) m)))
--       t
-- --
-- Î»Â» :example theorem 100
-- â†ª pair true (pair 50 refl)
-- : âˆ‘ (t : ð”¹) â†’
--   ð”¹-elim
--       (Î» _ â†’ ð“¤)
--       (âˆ‘ (m : â„•) â†’
--        Eq â„• 100 (â„•-elim (Î» _ â†’ â„•) 0 (Î» _ m â†’ S (S m)) m))
--       (âˆ‘ (m : â„•) â†’
--        Eq â„• 100 (S (â„•-elim
--                         (Î» _ â†’ â„•) 0 (Î» _ m â†’ S (S m)) m)))
--       t
-- --
-- Î»Â» :example theorem 113
-- â†ª pair false (pair 56 refl)
-- : âˆ‘ (t : ð”¹) â†’
--   ð”¹-elim
--       (Î» _ â†’ ð“¤)
--       (âˆ‘ (m : â„•) â†’
--        Eq â„• 113 (â„•-elim (Î» _ â†’ â„•) 0 (Î» _ m â†’ S (S m)) m))
--       (âˆ‘ (m : â„•) â†’
--        Eq â„• 113 (S (â„•-elim
--                         (Î» _ â†’ â„•) 0 (Î» _ m â†’ S (S m)) m)))
--       t
-- âˆŽ
--
evenOrOddScript :: forall s m. Script s m => m ()
evenOrOddScript = do
    section_ "double"

    define_ "succ"
        $$ TermNat ~> TermNat
        $$ lam_ "n" (Inf $ TermNatS "n")
    

    define_ "double"
        $$ TermNat ~> TermNat
        $$ lam_ "n" (Inf $ TermNatElim "_" (liftH TermNat)
            (Inf TermNatZ)
            (lams_ ["_","m"] ("succ" @@ ("succ" @@ "m")))
            "n")

    example_ $ "double" @@ 0
    example_ $ "double" @@ 1
    example_ $ "double" @@ 5

    section_ "Even"

    define_ "Even"
        $$ TermNat ~> sort_ typeSort
        $$ lam_ "n" (Inf $ Sigma "m" TermNat $ abstract1HSym "m" $ 
            Equality TermNat "n" ("double" @@ "m"))

    define_ "zero-is-even"
        $$ "Even" @@ 0
        $$ Pair 0 Refl
        
    example_ $$ Pair 1 Refl -:- "Even" @@ 2

    section_ "Odd"
      
    define_ "Odd"
        $$ TermNat ~> sort_ typeSort
        $$ lam_ "n" (Inf $ Sigma "m" TermNat $ abstract1HSym "m" $ 
            Equality TermNat "n" (Inf $ TermNatS $ "double" @@ "m"))

    example_ $$ Pair 3 Refl -:- "Odd" @@ 7

    let j_ u v w a p =  J (V3 (IrrSym u) (IrrSym v) (IrrSym w)) a (abstract3HSym u v w p)

    section_ "Congruence"

    define_ "congruence"
        $$ foralls_ ["A","B"] (pi_ "f" ("A"~> "B") $ pi_ "x" "A" $ pi_ "y" "A"
            $ Equality "A" "x" "y" ~> Equality "B" ("f" @@ "x") ("f" @@ "y"))
        $$ lams_ ["A","B","f","x","y","p"]
            (Inf $ j_ "u" "v" "_" "A" (Equality "B" ("f" @@ "u") ("f" @@ "v")) (lam_ "_" Refl) "x" "y" "p")

    section_ "Succ Even is Odd"

    define_ "succ-even-is-odd"
        $$ pi_ "n" TermNat ("Even" @@ "n" ~> "Odd" @@ (Inf $ TermNatS $ "n"))
        $$ lams_ ["n","even-n"] (Match "even-n" "m" "proof"
            $ abstract2HSym "m" "proof"
            $ Pair "m" $ "congruence" @@@ TermNat @@@ TermNat @@ "succ" @@ "n" @@ ("double" @@ "m") @@ "proof")

    define_ "succ-odd-is-even"
        $$ pi_ "n" TermNat ("Odd" @@ "n" ~> "Even" @@ (Inf $ TermNatS $ "n"))
        $$ lams_ ["n","odd-n"] (Match "odd-n" "m" "proof"
            $ abstract2HSym "m" "proof"
            $ Pair ("succ" @@ "m") $ "congruence" @@@ TermNat @@@ TermNat @@ "succ" @@ "n" @@ ("succ" @@ ("double" @@ "m")) @@ "proof")

    section_ "Boolean"

    define_ "if1"
        $$ pi_ "r" (sort_ typeSortSort) (TermBool ~> "r" ~> "r" ~> "r")
        $$ lams_ ["r", "b", "t", "f"]
              (Inf $ TermBoolElim "_" (liftH "r") "t" "f" "b")


    section_ "Either"

    subsection_ "Type"

    define_ "Either"
        $$ sort_ typeSort ~> sort_ typeSort ~> sort_ typeSort
        $$ lams_ ["A", "B"] (Inf $ Sigma "t" TermBool $ abstract1HSym "x" $ "if1" @@ sort_ typeSort @@ "x" @@ "A" @@ "B")

    subsection_ "Constructors"

    define_ "left"
        $$ foralls_ ["A", "B"] ("A" ~> "Either" @@ "A" @@ "B")
        $$ lams_ ["A","B","x"] (Pair (Inf TermTrue) "x")

    define_ "right"
        $$ foralls_ ["A", "B"] ("B" ~> "Either" @@ "A" @@ "B")
        $$ lams_ ["A","B","y"] (Pair (Inf TermFalse) "y")

    subsection_ "Non-dependent elimination"

    let eitherBody :: TermInf s Sym
        eitherBody = TermBoolElim
            "t" (abstract1HSym "t" $ "if1" @@ sort_ typeSort @@ "t" @@ "A" @@ "B" ~> "C")
            (lam_ "x" $ "f" @@ "x")
            (lam_ "y" $ "g" @@ "y")
            "t"

    define_ "either"
        $$ foralls_ ["A","B","C"] (("A" ~> "C") ~> ("B" ~> "C") ~> "Either" @@ "A" @@ "B" ~> "C")
        $$ lams_ ["A","B","C","f","g","e"]
            (Match "e" "t" "v" $ abstract2HSym "t" "v" $ eitherBody @@ "v")


    section_ "All number are either even or odd"

    let motive :: TermChk s Sym -> TermInf s Sym
        motive n = "Either" @@ ("Even" @@ n) @@ ("Odd" @@ n)

    define_ "theorem"
        $$ pi_ "n" TermNat (motive "n")
        $$ lam_ "n" (Inf $ TermNatElim "m" (abstract1HSym "m" $ motive "m")
            (Pair (Inf TermTrue) $ "zero-is-even")
            (lams_ ["l","H"] (Inf $ apps_ "either"
                [ "Even" @@ "l"
                , "Odd" @@ "l"
                , Inf (motive (Inf $ "succ" @@ "l"))
                , lam_ "p" $ Pair (Inf TermFalse) $ "succ-even-is-odd" @@ "l" @@ "p"
                , lam_ "p" $ Pair (Inf TermTrue)  $ "succ-odd-is-even" @@ "l" @@ "p"
                , "H"
                ]))
            "n")

    example_ $ "theorem" @@ 0
    example_ $ "theorem" @@ 100
    example_ $ "theorem" @@ 113

-- $setup
-- >>> :seti -XOverloadedStrings -XTypeApplications
-- >>> import Language.PTS.Pretty
-- >>> import Language.PTS.Systems
#endif
