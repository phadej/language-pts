{-# LANGUAGE OverloadedStrings, ScopedTypeVariables #-}
#ifdef LANGUAGE_PTS_HAS_EQUALITY
#ifdef LANGUAGE_PTS_HAS_BOOL
#ifdef LANGUAGE_PTS_HAS_NAT
#ifdef LANGUAGE_PTS_HAS_SIGMA
#ifdef LANGUAGE_PTS_HAS_PROP
#define EQUALITY_EXAMPLES
#endif
#endif
#endif
#endif
#endif
module Language.PTS.Examples.Equality (
#ifdef EQUALITY_EXAMPLES
    equalityScript,
    equivalenceScript,
    leibnizScript,
    inequalityScript,
#endif
    ) where

#ifdef EQUALITY_EXAMPLES
import Language.PTS
import Language.PTS.Bound

-- |
--
-- >>> runLoud $ spec_ (MartinLof 0) >> equalityScript
-- -- 1. Non-dependent elimination: if
-- -----------------------------------
-- --
-- Î»Â» :define if : âˆ€ r â†’ ð”¹ â†’ r â†’ r â†’ r
--               = Î» r b t f â†’ ð”¹-elim (Î» _ â†’ r) t f b
-- --
-- -- 2. Negation, not
-- -------------------
-- --
-- Î»Â» :define not : ð”¹ â†’ ð”¹ = Î» b â†’ if ð”¹ b false true
-- --
-- Î»Â» :example not
-- â†ª Î» b â†’ ð”¹-elim (Î» _ â†’ ð”¹) false true b : ð”¹ â†’ ð”¹
-- --
-- Î»Â» :example not true
-- â†ª false : ð”¹
-- --
-- Î»Â» :example not false
-- â†ª true : ð”¹
-- --
-- -- 3. Simple equality examples
-- ------------------------------
-- --
-- Î»Â» :example refl : Eq ð”¹ (not true) false
-- â†ª refl : Eq ð”¹ false false
-- --
-- Î»Â» :example refl : Eq ð”¹ (not false) true
-- â†ª refl : Eq ð”¹ true true
-- --
-- -- 4. Lemma: Not is involutive
-- ------------------------------
-- --
-- Î»Â» :define not-inv
-- : âˆ (b : ð”¹) â†’ Eq ð”¹ b (not (not b))
-- = Î» b â†’ ð”¹-elim (Î» x â†’ Eq ð”¹ x (not (not x))) refl refl b
-- --
-- -- 5. Example with exists
-- -------------------------
-- --
-- Î»Â» :define not-surj
-- : âˆ (x : ð”¹) â†’ âˆ‘ (y : ð”¹) â†’ Eq ð”¹ x (not y)
-- = Î» x â†’ ð”¹-elim (Î» b â†’ âˆ‘ (y : ð”¹) â†’ Eq ð”¹ b (not y))
--                (pair false refl)
--                (pair true refl)
--                x
-- âˆŽ
--
equalityScript :: forall s m. Script s m => m ()
equalityScript = do
    section_ "Non-dependent elimination: if"

    define_ "if"
        $$ forall_ "r" (TermBool ~> "r" ~> "r" ~> "r")
        $$ lams_ ["r", "b", "t", "f"]
              (Inf $ TermBoolElim "_" (liftH "r") "t" "f" "b")

    section_ "Negation, not"

    define_ "not"
        $$ TermBool ~> TermBool
        $$ lam_ "b" ("if" @@@ TermBool @@ "b" @@@ TermFalse @@@ TermTrue)

    example_ "not"
    example_ $ "not" @@@ TermTrue
    example_ $ "not" @@@ TermFalse

    section_ "Simple equality examples"

    example_ $$ Refl -:-
        Equality TermBool ("not" @@@ TermTrue) (Inf TermFalse)

    example_ $$ Refl -:-
        Equality TermBool ("not" @@@ TermFalse) (Inf TermTrue)

    section_ "Lemma: Not is involutive"

    let ty b = Equality TermBool b ("not" @@ ("not" @@ b))
    define_ "not-inv"
        $$ pi_ "b" TermBool (ty "b")
        $$ lam_ "b" (Inf $ TermBoolElim "x" (abstract1HSym "x" $ ty "x") Refl Refl "b")

    section_ "Example with exists"

    let ty2 :: TermInf s Sym -> TermInf s Sym
        ty2 x = sigma_ "y" TermBool (Equality TermBool (Inf x) ("not" @@ "y"))
    define_ "not-surj"
        $$ pi_ "x" TermBool (ty2 "x")
        $$ lam_ "x" (Inf $ TermBoolElim "b" (abstract1HSym "b" $ ty2 "b")
            (Pair (Inf TermFalse) Refl)
            (Pair (Inf TermTrue) Refl)
            "x")


-- | [OPLSS 2014 Type Theory](https://youtu.be/Wpls061J5D0?t=14340)
--
-- >>> runLoud $ spec_ (MartinLof 0) >> equivalenceScript
-- -- 1. Symmetry
-- --------------
-- --
-- Î»Â» :define SYM
-- : ð“¤ â†’ ð“¤ = Î» A â†’ âˆ (x : A) â†’ âˆ (y : A) â†’ Eq A x y â†’ Eq A y x
-- Î»Â» :define sym
-- : âˆ€ A â†’ SYM A
-- = Î» A x y p â†’ J A (Î» u v w â†’ Eq A v u) (Î» q â†’ refl) x y p
-- --
-- -- 1.1. Example
-- --
-- Î»Â» :define nat-fold
-- : âˆ€ r â†’ â„• â†’ (r â†’ r) â†’ r â†’ r
-- = Î» r n s z â†’ â„•-elim (Î» _ â†’ r) z (Î» l â†’ s) n
-- Î»Â» :define succ : â„• â†’ â„• = Î» n â†’ S n
-- Î»Â» :define plus : â„• â†’ â„• â†’ â„• = Î» x y â†’ nat-fold â„• x succ y
-- --
-- Î»Â» :example refl : Eq â„• (plus 3 1) (plus 1 3)
-- â†ª refl : Eq â„• 4 4
-- --
-- Î»Â» :example sym â„• (plus 3 1) (plus 1 3) refl
-- â†ª refl : Eq â„• 4 4
-- --
-- -- 2. Transitivity
-- ------------------
-- --
-- Î»Â» :define TRANS : ð“¤ â†’ ð“¤ = Î» A â†’ âˆ (x : A) â†’
--                                  âˆ (y : A) â†’
--                                  âˆ (z : A) â†’
--                                  Eq A x y â†’
--                                  Eq A y z â†’
--                                  Eq A x z
-- Î»Â» :define trans
-- : âˆ€ A â†’ TRANS A
-- = Î» A x y z p â†’
--       J A (Î» u v w â†’ Eq A v z â†’ Eq A u z) (Î» _ r â†’ r) x y p
-- --
-- Î»Â» :example trans â„• (plus 1 3) (plus 2 2) (plus 3 1) refl
-- â†ª Î» r â†’ r : Eq â„• 4 4 â†’ Eq â„• 4 4
-- âˆŽ
--
equivalenceScript :: forall s m. Script s m => m ()
equivalenceScript = do
    section_ "Symmetry"

    define_ "SYM"
        $$ sort_ typeSort ~> sort_ typeSort
        $$ lam_ "A" (pi_ "x" "A" $ pi_ "y" "A" $ Equality "A" "x" "y" ~> Equality "A" "y" "x")

    let j_ u v w a p =  J (V3 (IrrSym u) (IrrSym v) (IrrSym w)) a (abstract3HSym u v w p)

    define_ "sym"
        $$ forall_ "A" ("SYM" @@ "A")
        $$ lams_ ["A","x","y","p"]
            (Inf $ j_ "u" "v" "w" "A" (Equality "A" "v" "u") (lam_ "q" Refl) "x" "y" "p")

    subsection_ "Example"

    define_ "nat-fold"
        $$ forall_ "r" (TermNat ~> ("r" ~> "r") ~> "r" ~> "r")
        $$ lams_ ["r", "n", "s", "z"]
              (Inf $ TermNatElim "_" (liftH "r") "z" (lam_ "l" "s") "n")

    define_ "succ"
        $$ TermNat ~> TermNat
        $$ lam_ "n" (Inf $ TermNatS "n")

    define_ "plus"
        $$ TermNat ~> TermNat ~> TermNat
        $$ lams_ ["x", "y"] ("nat-fold" @@@ TermNat @@ "x" @@ "succ" @@ "y")

    example_ $ Refl -:- Equality TermNat (3 + 1) (1 + 3)
    example_ $ "sym" @@@ TermNat @@ 3 + 1 @@ 1 + 3 @@ Refl

    section_ "Transitivity"

    define_ "TRANS"
        $$ sort_ typeSort ~> sort_ typeSort
        $$ lam_ "A" (pi_ "x" "A" $ pi_ "y" "A" $ pi_ "z" "A" $ Equality "A" "x" "y" ~> Equality "A" "y" "z" ~> Equality "A" "x" "z")

    define_ "trans"
        $$ forall_"A" ("TRANS" @@ "A")
        $$ lams_ ["A","x","y","z","p"]
            (Inf $ j_ "u" "v" "w" "A" (Equality "A" "v" "z" ~> Equality "A" "u" "z") (lams_ ["_", "r"] "r") "x" "y" "p")

    example_ $ "trans" @@@ TermNat @@ 1 + 3 @@ 2 + 2 @@ 3 + 1 @@ Refl

-- |
--
-- >>> runLoud $ spec_ (MartinLof 0) >> inequalityScript
-- -- Proving inequalities is difficult!
-- Î»Â» :define ifâ‚ : âˆ (r : ð“¤â‚) â†’ ð”¹ â†’ r â†’ r â†’ r
--                = Î» r b t f â†’ ð”¹-elim (Î» _ â†’ r) t f b
-- --
-- -- Important thing is to find proper motive for induction
-- Î»Â» :define motive
-- : ð”¹ â†’ ð”¹ â†’ ð“¤ = Î» u v â†’ ifâ‚ ð“¤ u (ifâ‚ ð“¤ v âŠ¤ âŠ¥) âŠ¤
-- --
-- -- With proper motive, proof is almost trivial
-- Î»Â» :define true-is-not-false
-- : Eq ð”¹ true false â†’ âŠ¥
-- = Î» p â†’ J ð”¹
--           (Î» u v w â†’ motive u v)
--           (Î» b â†’ ð”¹-elim (Î» c â†’ motive c c) I I b)
--           true
--           false
--           p
-- --
-- Î»Â» :example true-is-not-false
-- â†ª Î» p â†’
--       J ð”¹
--         (Î» u v w â†’
--              ð”¹-elim (Î» _ â†’ ð“¤) (ð”¹-elim (Î» _ â†’ ð“¤) âŠ¤ âŠ¥ v) âŠ¤ u)
--         (Î» b â†’ ð”¹-elim (Î» c â†’ ð”¹-elim (Î» _ â†’ ð“¤)
--                                     (ð”¹-elim (Î» _ â†’ ð“¤) âŠ¤ âŠ¥ c)
--                                     âŠ¤
--                                     c)
--                       I
--                       I
--                       b)
--         true
--         false
--         p
-- : Eq ð”¹ true false â†’ âŠ¥
-- âˆŽ
--
inequalityScript :: forall s m. Script s m => m ()
inequalityScript = do
    comment_ "Proving inequalities is difficult!"

    define_ "if1"
        $$ pi_ "r" (sort_ typeSortSort) (TermBool ~> "r" ~> "r" ~> "r")
        $$ lams_ ["r", "b", "t", "f"]
              (Inf $ TermBoolElim "_" (liftH "r") "t" "f" "b")

    comment_ "Important thing is to find proper motive for induction"

    let motive u v = "if1" @@ sort_ typeSort @@ u @@ ("if1" @@ sort_ typeSort @@ v @@@ Unit @@@ Empty) @@@ Unit

    define_ "motive"
        $$ TermBool ~> TermBool ~> sort_ typeSort
        $$ lams_ ["u","v"] (motive "u" "v")

    comment_ "With proper motive, proof is almost trivial"
    let j_ u v w a p =  J (V3 (IrrSym u) (IrrSym v) (IrrSym w)) a (abstract3HSym u v w p)

    define_ "true-is-not-false"
        $$ Equality TermBool (Inf TermTrue) (Inf TermFalse) ~> Empty
        $$ lams_ ["p"]
            (Inf $ j_ "u" "v" "w" TermBool
                ("motive" @@ "u" @@  "v")
                (lam_ "b" $ Inf $ TermBoolElim "c" (abstract1HSym "c" $ "motive" @@ "c" @@ "c") (Inf I) (Inf I) "b")
                (Inf TermTrue) (Inf TermFalse) "p")

    example_ "true-is-not-false"

-- |
--
-- >>> runLoud $ spec_ CoCStar >> leibnizScript
-- -- 1. Leibniz
-- -------------
-- --
-- -- We can define Leibniz equality
-- -- in the systems with impredicative bottom universe.
-- -- TODO: define CComega, and make conversions
-- Î»Â» :define Leibniz
-- : âˆ€ A â†’ A â†’ A â†’ â‹† = Î» A x y â†’ âˆ (C : (A â†’ â‹†)) â†’ C x â†’ C y
-- --
-- -- 1.1. Reflexivity
-- --
-- Î»Â» :define REFL : â‹† â†’ â‹† = Î» A â†’ âˆ (x : A) â†’ Leibniz A x x
-- Î»Â» :define reflâ‚ : âˆ€ A â†’ REFL A = Î» A x C Cx â†’ Cx
-- --
-- -- 1.2. Symmetry
-- --
-- Î»Â» :define SYM : â‹† â†’ â‹† = Î» A â†’ âˆ (x : A) â†’
--                                âˆ (y : A) â†’
--                                Leibniz A x y â†’
--                                Leibniz A y x
-- Î»Â» :define sym
-- : âˆ€ A â†’ SYM A = Î» A x y xy â†’ xy (Î» z â†’ Leibniz A z x)
--                                 (reflâ‚ A x)
-- --
-- -- 1.3. Transitivity
-- --
-- -- An exercise!
-- âˆŽ
--
leibnizScript :: forall s m. Script s m => m ()
leibnizScript = do
    section_ "Leibniz"

    comment_ "We can define Leibniz equality"
    comment_ "in the systems with impredicative bottom universe."
    comment_ "TODO: define CComega, and make conversions"

    define_ "Leibniz"
        $$ pi_ "A" (sort_ typeSort) ("A" ~> "A" ~> sort_ typeSort)
        -- TODO: change last @@ to ~>, improve error message
        $$ lams_ ["A","x","y"] (pi_ "C" ("A" ~> sort_ typeSort) $ "C" @@ "x" ~> "C" @@ "y")

    subsection_ "Reflexivity"

    define_ "REFL"
        $$ sort_ typeSort ~> sort_ typeSort
        $$ lam_ "A" (pi_ "x" "A" $ "Leibniz" @@ "A" @@ "x" @@ "x")

    define_ "refl1"
        $$ forall_ "A" ("REFL" @@ "A")
        $$ lams_ ["A","x","C","Cx"] "Cx"

    subsection_ "Symmetry"

    define_ "SYM"
        $$ sort_ typeSort ~> sort_ typeSort
        $$ lam_ "A" (pi_ "x" "A" $ pi_ "y" "A" $ "Leibniz" @@ "A" @@ "x" @@ "y" ~> "Leibniz" @@ "A" @@ "y" @@ "x")

    define_ "sym"
        $$ forall_ "A" ("SYM" @@ "A")
        $$ lams_ ["A","x","y","xy"]
            ("xy" @@ lam_ "z" ("Leibniz" @@ "A" @@ "z" @@ "x") @@ ("refl1" @@ "A" @@ "x"))

    subsection_ "Transitivity"

    comment_ "An exercise!"

#endif

-- $setup
-- >>> :seti -XOverloadedStrings -XTypeApplications
-- >>> import Language.PTS.Pretty
-- >>> import Language.PTS.Systems
