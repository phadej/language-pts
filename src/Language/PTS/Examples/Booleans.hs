{-# LANGUAGE OverloadedStrings #-}
module Language.PTS.Examples.Booleans (
    churchBooleansScript,
#if defined(LANGUAGE_PTS_HAS_BOOL) && defined(LANGUAGE_PTS_HAS_NAT)
    booleansScript,
#endif
    ) where

import Language.PTS

#if defined(LANGUAGE_PTS_HAS_BOOL) && defined(LANGUAGE_PTS_HAS_NAT)
import Control.Monad.Trans.Class (lift)

import Language.PTS.Bound
#endif

-------------------------------------------------------------------------------
-- Church Booleans
-------------------------------------------------------------------------------

-- | 'SystemF' is powerful enough to define Church Booleans.
--
-- >>> runLoud $ spec_ SysFStar >> churchBooleansScript
-- -- 1. Definitions
-- -----------------
-- --
-- Î»Â» :define Bool : â‹† = âˆ€ r â†’ r â†’ r â†’ r
-- Î»Â» :define True : Bool = Î» r t f â†’ t
-- Î»Â» :define False : Bool = Î» r t f â†’ f
-- --
-- -- 2. Functions
-- ---------------
-- --
-- -- Bool values are itself an if statement
-- Î»Â» :define not : Bool â†’ Bool = Î» x â†’ x Bool False True
-- Î»Â» :define and : Bool â†’ Bool â†’ Bool = Î» x y â†’ x Bool y False
-- --
-- -- 3. Examples
-- --------------
-- --
-- -- One have to look carefully to distinguish the results :)
-- Î»Â» :example and True True
-- â†ª Î» r t f â†’ t : âˆ€ r â†’ r â†’ r â†’ r
-- --
-- Î»Â» :example and True False
-- â†ª Î» r t f â†’ f : âˆ€ r â†’ r â†’ r â†’ r
-- --
-- Î»Â» :example and False True
-- â†ª Î» r t f â†’ f : âˆ€ r â†’ r â†’ r â†’ r
-- --
-- -- 4. Extras
-- ------------
-- --
-- -- Note the usage of impredicativity.
-- Î»Â» :example not
-- â†ª Î» x â†’ x (âˆ€ r â†’ r â†’ r â†’ r) (Î» r t f â†’ f) (Î» r t f â†’ t)
-- : (âˆ€ r â†’ r â†’ r â†’ r) â†’ âˆ€ r â†’ r â†’ r â†’ r
-- --
-- Î»Â» :example and
-- â†ª Î» x y â†’ x (âˆ€ r â†’ r â†’ r â†’ r) y (Î» r t f â†’ f)
-- : (âˆ€ r â†’ r â†’ r â†’ r) â†’ (âˆ€ r â†’ r â†’ r â†’ r) â†’ âˆ€ r â†’ r â†’ r â†’ r
-- âˆŽ
--
churchBooleansScript :: Script s m => m ()
churchBooleansScript = do
    section_ "Definitions"

    define_ "Bool"
        $$ sort_ typeSort
        $$ forall_ "r" ("r" ~> "r" ~> "r")

    define_ "True"
        $$ "Bool"
        $$ lams_ ["r", "t", "f"] "t"

    define_ "False"
        $$ "Bool"
        $$ lams_ ["r", "t", "f"] "f"

    section_ "Functions"

    comment_ "Bool values are itself an if statement"
    define_ "not"
        $$ "Bool" ~> "Bool"
        $$ lam_ "x" ("x" @@ "Bool" @@ "False" @@ "True")

    define_ "and"
        $$ "Bool" ~> "Bool" ~> "Bool"
        $$ lams_ ["x", "y"] ("x" @@ "Bool" @@ "y" @@ "False")

    section_ "Examples"

    comment_ "One have to look carefully to distinguish the results :)"
    example_ $ "and" @@ "True"  @@ "True"
    example_ $ "and" @@ "True"  @@ "False"
    example_ $ "and" @@ "False" @@ "True"

    section_ "Extras"
    comment_ "Note the usage of impredicativity."

    example_ "not"
    example_ "and"

#if defined(LANGUAGE_PTS_HAS_BOOL) && defined(LANGUAGE_PTS_HAS_NAT)
-------------------------------------------------------------------------------
-- Built-in Booleans
-------------------------------------------------------------------------------

-- | Examples of built-in Booleans.
-- We need dependent types to be able to use dependent @ð”¹-elim@.
--
-- >>> runLoud $ spec_ (MartinLof 0) >> booleansScript
-- -- 1. Constants
-- ---------------
-- --
-- Î»Â» :example ð”¹
-- â†ª ð”¹ : ð“¤
-- --
-- Î»Â» :example true
-- â†ª true : ð”¹
-- --
-- Î»Â» :example false
-- â†ª false : ð”¹
-- --
-- -- 2. Non-dependent elimination: if
-- -----------------------------------
-- --
-- Î»Â» :define if : âˆ€ r â†’ ð”¹ â†’ r â†’ r â†’ r
--               = Î» r b t f â†’ ð”¹-elim (Î» _ â†’ r) t f b
-- --
-- -- 3. Some simple functions on Booleans
-- ---------------------------------------
-- --
-- -- 3.1. Negation, not
-- --
-- Î»Â» :define not : ð”¹ â†’ ð”¹ = Î» b â†’ if ð”¹ b false true
-- --
-- Î»Â» :example not
-- â†ª Î» b â†’ ð”¹-elim (Î» _ â†’ ð”¹) false true b : ð”¹ â†’ ð”¹
-- --
-- Î»Â» :example not true
-- â†ª false : ð”¹
-- --
-- Î»Â» :example not false
-- â†ª true : ð”¹
-- --
-- -- 3.2. Conjunction, and
-- --
-- Î»Â» :define and : ð”¹ â†’ ð”¹ â†’ ð”¹ = Î» x y â†’ if ð”¹ x y false
-- --
-- Î»Â» :example and
-- â†ª Î» x y â†’ ð”¹-elim (Î» _ â†’ ð”¹) y false x : ð”¹ â†’ ð”¹ â†’ ð”¹
-- --
-- Î»Â» :example and true true
-- â†ª true : ð”¹
-- --
-- Î»Â» :example and true false
-- â†ª false : ð”¹
-- --
-- Î»Â» :example and false true
-- â†ª false : ð”¹
-- --
-- Î»Â» :example and false false
-- â†ª false : ð”¹
-- --
-- -- 4. Using dependent elimination
-- ---------------------------------
-- --
-- Î»Â» :define contrived
-- : Î  (b : ð”¹) â†’ ð”¹-elim (Î» _ â†’ ð“¤) ð”¹ â„• b
-- = Î» b â†’ ð”¹-elim (Î» p â†’ ð”¹-elim (Î» _ â†’ ð“¤) ð”¹ â„• p) true 0 b
-- --
-- Î»Â» :example contrived
-- â†ª Î» b â†’ ð”¹-elim (Î» p â†’ ð”¹-elim (Î» _ â†’ ð“¤) ð”¹ â„• p) true 0 b
-- : Î  (b : ð”¹) â†’ ð”¹-elim (Î» _ â†’ ð“¤) ð”¹ â„• b
-- --
-- Î»Â» :example contrived true
-- â†ª true : ð”¹
-- --
-- Î»Â» :example contrived false
-- â†ª 0 : â„•
-- âˆŽ
--
booleansScript :: Script s m => m ()
booleansScript = do
    section_ "Constants"

    example_ TermBool
    example_ TermTrue
    example_ TermFalse

    section_ "Non-dependent elimination: if"

    define_ "if"
        $$ forall_ "r" (TermBool ~> "r" ~> "r" ~> "r")
        $$ lams_ ["r", "b", "t", "f"]
              (Inf $ TermBoolElim "_" (lift "r") "t" "f" "b")

    section_ "Some simple functions on Booleans"
    subsection_ "Negation, not"

    define_ "not"
        $$ TermBool ~> TermBool
        $$ lam_ "b" ("if" @@@ TermBool @@ "b" @@@ TermFalse @@@ TermTrue)

    example_ "not"
    example_ $ "not" @@@ TermTrue
    example_ $ "not" @@@ TermFalse

    subsection_ "Conjunction, and"

    define_ "and"
        $$ TermBool ~> TermBool ~> TermBool
        $$ lams_ ["x", "y"] ("if" @@@ TermBool @@ "x" @@ "y" @@@ TermFalse)

    example_ "and"
    example_ $ "and" @@@ TermTrue  @@@ TermTrue
    example_ $ "and" @@@ TermTrue  @@@ TermFalse
    example_ $ "and" @@@ TermFalse @@@ TermTrue
    example_ $ "and" @@@ TermFalse @@@ TermFalse

    -- TODO: change to truth
    section_ "Using dependent elimination"

    let ty = TermBoolElim "_" (lift $ sort_ typeSort) (Inf TermBool) (Inf TermNat) "b"
    define_ "contrived"
        $$ pi_ "b" TermBool
              (TermBoolElim "_" (lift $ sort_ typeSort) (Inf TermBool) (Inf TermNat) "b")
        $$ lam_ "b"
            (Inf $ TermBoolElim "p" (abstract1Sym "b" ty) (Inf TermTrue) (Inf TermNatZ)  "b")

    example_ "contrived"
    example_ $ "contrived" @@@ TermTrue
    example_ $ "contrived" @@@ TermFalse
#endif

-- $setup
-- >>> :seti -XOverloadedStrings -XTypeApplications
-- >>> import Language.PTS.Pretty
-- >>> import Language.PTS.Systems
