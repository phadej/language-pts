{-# LANGUAGE OverloadedStrings   #-}
{-# LANGUAGE ScopedTypeVariables #-}
module Language.PTS.Examples.Hurkens (
    hurkensScript,
    defineU,
    ) where

import Data.String  (IsString)
import Language.PTS

-- | /A Simplification of Girard's Paradox/ by Hurkens (TLCA '95)
--
-- Hurkens' version fails in HOL, there are no (â–³,â–¡,â–¡)-rule:
--
-- >>> runSilent $ spec_ HOLStar >> hurkensScript
-- Î»Â» :define âŠ¥ : â‹† = âˆ€ x â†’ x
-- Î»Â» :define Â¬ : â‹† â†’ â‹† = Î» P â†’ P â†’ âŠ¥
-- Î»Â» :define U : â–¡ = âˆ (X : â–¡) â†’ (((X â†’ â‹†)..
-- error:
-- â€¢ No PTS Rule (â–³,â–¡,-)
-- â€¢ when checking expression
--   âˆ (X : â–¡) â†’ (((X â†’ â‹†) â†’ â‹†) â†’ X) â†’ (X â†’ â‹†) â†’ â‹†
--
-- It fails even sooner in predicative systems, because we cannot define âŠ¥
-- purely; to begin with:
--
-- >>> runSilent $ spec_ (MartinLof 0) >> hurkensScript
-- Î»Â» :define âŠ¥ : ð“¤ = âˆ€ x â†’ x
-- error:
-- â€¢ Couldn't match expected type ð“¤ with actual type ð“¤â‚
-- â€¢ In the expression: âˆ€ x â†’ x
--
-- ...or if we try to define @U@, it will be in the wrong universe:
--
-- >>> runSilent $ spec_ (MartinLof 0) >> defineU
-- Î»Â» :define U : ð“¤â‚ = âˆ (X : ð“¤â‚) â†’ (((X â†’ ð“¤)..
-- error:
-- â€¢ Couldn't match expected type ð“¤â‚ with actual type ð“¤â‚‚
-- â€¢ In the expression: âˆ (X : ð“¤â‚) â†’ (((X â†’ ð“¤) â†’ ð“¤) â†’ X) â†’ (X â†’ ð“¤) â†’ ð“¤
--
-- __However__ in System U, the script goes through:
--
-- >>> runSilent $ spec_ SysUStar >> hurkensScript
-- Î»Â» :define âŠ¥ : â‹† = âˆ€ x â†’ x
-- Î»Â» :define Â¬ : â‹† â†’ â‹† = Î» P â†’ P â†’ âŠ¥
-- Î»Â» :define U : â–¡ = âˆ (X : â–¡) â†’ (((X â†’ â‹†)..
-- Î»Â» :define Î” : U â†’ â‹† = Î» y â†’ Â¬ (âˆ (p : (U â†’..
-- Î»Â» :define Î© : U = Î» X f p â†’ (Î» pâ‚ â†’ âˆ (x..
-- Î»Â» :define Î˜ : â‹† = âˆ (p : (U â†’ â‹†)) â†’ (âˆ..
-- Î»Â» :define D : â‹† = âˆ (p : (U â†’ â‹†)) â†’ Î© U..
-- Î»Â» :define lemma : Î˜ = Î» p tâ‚ â†’ tâ‚ Î© (Î» x â†’..
-- Î»Â» :define H : D = Î» p â†’ lemma (Î» y â†’ p..
-- Î»Â» :define negH : Â¬ D = lemma Î” (Î» x Hâ‚‚ Hâ‚ƒ â†’..
-- Î»Â» :define Â¡Ay, caramba! : âŠ¥ = negH H
-- âˆŽ
--
hurkensScript :: forall s m. Script s m => m ()
hurkensScript = do
    -- Unicode cheating
    let bot, neg, delta, omega, theta :: IsString a => a
        bot = "âŠ¥"
        neg = "Â¬"
        delta = "Î”"
        omega = "Î©"
        theta = "Î˜"

    let tstar, tbox :: CanSort u => u s a
        tstar = sort_ typeSort
        tbox  = sort_ typeSortSort

    section_ "Falsehood and negation"

    define_ bot
        $$ tstar
        $$ forall_ "x" "x"

    define_ neg
        $$ tstar ~> tstar
        $$ lam_ "P" ("P" ~> bot)

    example_ bot
    example_ neg

    section_ "Power set and paradoxial universe"

    comment_ "we need (â–³,â–³,â–³) to define â„˜ S = S â†’ â‹†"
    comment_ "luckily we have Haskell as the meta-language"
    let power :: TermInf s a -> TermInf s a
        power s = s ~> tstar

    define_ "U"
        $$ tbox
        $$ pi_ "X" tbox ((power (power "X") ~> "X") ~> power (power "X"))

    example_   "U"
    example_ $ power "U"
    example_ $ power (power "U")

    section_ "Pseudo-terms"

    comment_ "Ï„ : â„˜â„˜U â†’ U"
    let tau :: TermInf s Sym -> TermInf s Sym
        tau t =
            lams_ ["X", "f", "p"] (t @@ lam_ "x" (Inf $ "p" @@ ("f" @@ ("x" @@ "X" @@ "f"))))
            -:- "U"

    comment_ "Ïƒ : U â†’ â„˜â„˜U"
    let sigma :: TermInf s Sym -> TermInf s Sym
        sigma s =
            (Inf $ s @@ "U" @@ lam_ "t" (Inf (tau "t")))
            -:- power (power "U")

    let tauSigma = tau . sigma

    section_ "Normal terms"

    define_ delta
        $$ power "U"
        $$ lam_ "y" (neg @@ pi_ "p" (power "U") (sigma "y" @@ "p" ~> "p" @@@ tauSigma "y"))

    define_ omega
        $$ "U"
        $$ Inf (tau (lam_ "p" (pi_ "x" "U" (sigma "x" @@ "p" ~> "p" @@ "x")) -:- power (power "U")))

    define_ theta
        $$ tstar
        $$ Inf (pi_ "p" (power "U") (pi_ "x" "U" (sigma "x" @@ "p" ~> "p" @@ "x") ~> "p" @@ omega))

    define_ "D"
        $$ tstar
        $$ Inf (pi_ "p" (power "U") (sigma omega @@ "p" ~> "p" @@@ tauSigma omega))

    example_ delta
    example_ omega
    example_ theta
    example_ "D"

    section_ "Lemmas"

    define_ "lemma"
        $$ theta
        $$ lams_ ["p", "t1"] ("t1" @@ omega @@ lam_ "x" (Inf ("t1" @@@ tauSigma "x")))

    define_ "H"
        $$ "D"
        $$ lam_ "p" ("lemma" @@ lam_ "y" ("p" @@@ tauSigma "y"))

    define_ "negH"
        $$ neg @@ "D"
        $$ "lemma" @@ delta @@ lams_ ["x", "H2", "H3"]
            ("H3" @@ delta @@ "H2" @@ lam_ "pp" ("H3" @@ lam_ "y" ("pp" @@@ tauSigma "y")))

    section_ "Falsehood evidence"

    define_ "Â¡Ay, caramba!" $$ bot $$ "negH" @@ "H"

defineU :: forall s m. Script s m => m ()
defineU = do
    let tstar, tbox :: CanSort u => u s a
        tstar = sort_ typeSort
        tbox  = sort_ typeSortSort

    section_ "Power set and paradoxial universe"

    comment_ "we need (â–³,â–³,â–³) to define â„˜ S = S â†’ â‹†"
    comment_ "luckily we have Haskell as the meta-language"
    let power :: TermInf s a -> TermInf s a
        power s = s ~> tstar

    define_ "U"
        $$ tbox
        $$ pi_ "X" tbox ((power (power "X") ~> "X") ~> power (power "X"))

-- $setup
-- >>> :set -XOverloadedStrings -XTypeApplications
-- >>> import Language.PTS
-- >>> import Language.PTS.Pretty
-- >>> import Language.PTS.Systems
